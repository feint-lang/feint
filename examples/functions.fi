self_referential = (n) ->
    if n == 0 ->
        [0]
    else ->
        items = self_referential(n - 1)
        # NOTE: `List.extend()` returns `this`
        items.extend((n,))

print(self_referential(3))  # -> [0, 1, 2, 3]

# Same as above but using match
f = (n) ->
    match n ->
        0 -> [0]
        : -> f(n - 1).extend([n])

print(f(3))  # -> [0, 1, 2, 3]

returns = (n) ->
    if n > 1 ->
        return "explicit return"
    "implicit return"

print(returns(2))  # -> "explicit"
print(returns(1))  # -> "implicit"

# In the assigment `x = x`, the RHS x refers to the global x
x = 1
f = () ->
    x = x
    x += 2
print(f(), "(x in f() should be 3)")  # -> 3
print(x, "(global x should be 1)")    # -> 1

# Var args allowed in last position
f = (...) -> print($args.length, "var args")
f(1, 2, 3)  # -> 3

f = (required, ...) -> print($args.length, "var args")
f(1, 2, 3)  # -> 2
